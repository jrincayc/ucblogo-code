TO #GATHER :SEN
IF EMPTYP :SEN [OP :SEN]
IF NOT TRY.PRED [OP :SEN]
MAKE :SPECIAL.VAR LPUT FIRST :SEN THING :SPECIAL.VAR
OP #GATHER BF :SEN
END

TO #TEST :SEN
IF MATCH BF :PAT :SEN [OP "TRUE]
IF EMPTYP THING :SPECIAL.VAR [OP "FALSE]
OP #TEST2 FPUT LAST THING :SPECIAL.VAR :SEN
END

TO #TEST2 :SEN
MAKE :SPECIAL.VAR BL THING :SPECIAL.VAR
OP #TEST :SEN
END

TO &TEST :TF
IF EMPTYP THING :SPECIAL.VAR [OP "FALSE]
OP :TF
END

TO @TEST :SEN
IF @TRY.PRED [IF MATCH BF :PAT :SEN [OP "TRUE]]
IF EMPTYP THING :SPECIAL.VAR [OP "FALSE]
OP @TEST2 FPUT LAST THING :SPECIAL.VAR :SEN
END

TO @TEST2 :SEN
MAKE :SPECIAL.VAR BL THING :SPECIAL.VAR
OP @TEST :SEN
END

TO @TRY.PRED
IF LISTP :SPECIAL.PRED [OP MATCH :SPECIAL.PRED THING :SPECIAL.VAR]
OP RUN LIST :SPECIAL.PRED THING :SPECIAL.VAR
END

TO ADDPUNCT :STUFF :CHAR
IF WORDP :STUFF [OUTPUT WORD :STUFF :CHAR]
IF EMPTYP :STUFF [OUTPUT :CHAR]
OUTPUT SE BL :STUFF WORD LAST :STUFF :CHAR
END

TO ALWAYS :X
OP "TRUE
END

TO ANALYZE :SENTENCE :KEYWORDS
LOCAL [RULES KEYWORD]
IF EMPTYP :KEYWORDS [NORULES STOP]
MAKE "KEYWORD FIRST :KEYWORDS
MAKE "RULES GPROP :KEYWORD "RULES
IF WORDP FIRST :RULES ~
   [MAKE "KEYWORD FIRST :RULES MAKE "RULES GPROP :KEYWORD "RULES]
CHECKRULES :KEYWORD :RULES
END

TO ANYOF :SEN
OP ANYOF1 :SEN :IN.LIST
END

TO ANYOF1 :SEN :PATS
IF EMPTYP :PATS [OP "FALSE]
IF MATCH FIRST :PATS :SEN [OP "TRUE]
OP ANYOF1 :SEN BF :PATS
END

TO BELIEFP :WORD
OUTPUT NOT EMPTYP GPROP :WORD "BELIEF
END

TO CHECKPRIORITY :WORD
LOCAL "PRIORITY
MAKE "PRIORITY GPROP :WORD "PRIORITY
IF EMPTYP :PRIORITY [STOP]
IF EMPTYP :KEYWORDS [MAKE "KEYWORDS ( LIST :WORD ) STOP]
IFELSE :PRIORITY > ( GPROP FIRST :KEYWORDS "PRIORITY ) ~
       [MAKE "KEYWORDS FPUT :WORD :KEYWORDS] ~
       [MAKE "KEYWORDS LPUT :WORD :KEYWORDS]
END

TO CHECKRULES :KEYWORD :RULES
IF NOT MATCH FIRST :RULES :SENTENCE ~
   [CHECKRULES :KEYWORD BF BF :RULES STOP]
DORULE FIRST BF :RULES
END

TO DOCTOR
LOCAL [TEXT SENTENCE STUFF A B C RULES KEYWORDS]
MAKE "MEMORY []
PR [HELLO, I AM THE DOCTOR. WHAT CAN I DO FOR YOU?]
PR [PLEASE END YOUR REMARKS WITH AN EMPTY LINE.]
PR []
LOOP
END

TO DORULE :RULE
LOCAL "PRINT
MAKE "PRINT FIRST GPROP :KEYWORD :RULE
PPROP :KEYWORD :RULE LPUT :PRINT BF GPROP :KEYWORD :RULE
IF EQUALP :PRINT "NEWKEY [ANALYZE :SENTENCE BF :KEYWORDS STOP]
IF WORDP :PRINT [CHECKRULES :PRINT GPROP :PRINT "RULES STOP]
IF EQUALP FIRST :PRINT "PRE ~
   [ANALYZE RECONSTRUCT FIRST BF :PRINT BF BF :PRINT STOP]
PRINT RECONSTRUCT :PRINT
MEMORY :KEYWORD :SENTENCE
END

TO FAMILYP :WORD
OUTPUT NOT EMPTYP GPROP :WORD "FAMILY
END

TO GETSENTENCE :TEXT
MAKE "KEYWORDS []
OUTPUT GETSENTENCE1 :TEXT []
END

TO GETSENTENCE1 :TEXT :OUT
IF EMPTYP :TEXT [OUTPUT :OUT]
IF EQUALP FIRST :TEXT ". ~
   [IFELSE EMPTYP :KEYWORDS ~
           [OUTPUT GETSENTENCE1 BF :TEXT []] [OUTPUT :OUT]]
CHECKPRIORITY FIRST :TEXT
OUTPUT GETSENTENCE1 BF :TEXT SE :OUT TRANSLATE FIRST :TEXT
END

TO GETSTUFF :STUFF
LOCAL "LINE
MAKE "LINE RL
IF EMPTYP :LINE [OP :STUFF]
OP GETSTUFF SE :STUFF :LINE
END

TO IN :WORD
OP MEMBERP :WORD :IN.LIST
END

TO LASTRESORT
PRINT FIRST :LASTRESORT
MAKE "LASTRESORT LPUT FIRST :LASTRESORT BF :LASTRESORT
END

TO LOOP
MAKE "TEXT TOKENIZE GETSTUFF []
MAKE "SENTENCE GETSENTENCE :TEXT
ANALYZE :SENTENCE :KEYWORDS
PRINT []
LOOP
END

TO MATCH :PAT :SEN
LOCAL [SPECIAL.VAR SPECIAL.PRED SPECIAL.BUFFER IN.LIST]
IF OR WORDP :PAT WORDP :SEN [OP "FALSE]
IF EMPTYP :PAT [OP EMPTYP :SEN]
IF LISTP FIRST :PAT [OP SPECIAL FPUT "!: :PAT :SEN]
IF MEMBERP FIRST FIRST :PAT [? # ! & @] [OP SPECIAL :PAT :SEN]
IF EMPTYP :SEN [OP "FALSE]
IF EQUALP FIRST :PAT FIRST :SEN [OP MATCH BF :PAT BF :SEN]
OP "FALSE
END

TO MATCH!
IF EMPTYP :SEN [OP "FALSE]
IF NOT TRY.PRED [OP "FALSE]
MAKE :SPECIAL.VAR FIRST :SEN
OP MATCH BF :PAT BF :SEN
END

TO MATCH#
MAKE :SPECIAL.VAR []
OP #TEST #GATHER :SEN
END

TO MATCH&
OP &TEST MATCH#
END

TO MATCH?
MAKE :SPECIAL.VAR []
IF EMPTYP :SEN [OP MATCH BF :PAT :SEN]
IF NOT TRY.PRED [OP MATCH BF :PAT :SEN]
MAKE :SPECIAL.VAR FIRST :SEN
IF MATCH BF :PAT BF :SEN [OP "TRUE]
MAKE :SPECIAL.VAR []
OP MATCH BF :PAT :SEN
END

TO MATCH@
MAKE :SPECIAL.VAR :SEN
OP @TEST []
END

TO MEMORY :KEYWORD :SENTENCE
LOCAL [RULES RULE NAME]
MAKE "RULES GPROP :KEYWORD "MEMR
IF EMPTYP :RULES [STOP]
IF NOT MATCH FIRST :RULES :SENTENCE [STOP]
MAKE "NAME LAST :RULES
MAKE "RULES GPROP :KEYWORD :NAME
MAKE "RULE FIRST :RULES
PPROP :KEYWORD :NAME LPUT :RULE BF :RULES
MAKE "MEMORY FPUT RECONSTRUCT :SENTENCE :MEMORY
END

TO NORULES
IFELSE :MEMFLAG [USEMEMORY] [LASTRESORT]
MAKE "MEMFLAG NOT :MEMFLAG
END

TO PARSE.SPECIAL :WORD :VAR
IF EMPTYP :WORD [OP LIST :VAR "ALWAYS]
IF EQUALP FIRST :WORD ": [OP LIST :VAR BF :WORD]
OP PARSE.SPECIAL BF :WORD WORD :VAR FIRST :WORD
END

TO QUOTED :THING
IF LISTP :THING [OP :THING]
OP WORD "" :THING
END

TO RECONSTRUCT :SENTENCE
IF EMPTYP :SENTENCE [OUTPUT []]
IF NOT EQUALP ": FIRST FIRST :SENTENCE ~
   [OUTPUT FPUT FIRST :SENTENCE RECONSTRUCT BF :SENTENCE]
OUTPUT SE REWORD FIRST :SENTENCE RECONSTRUCT BF :SENTENCE
END

TO REWORD :WORD
IF MEMBERP LAST :WORD [. ? ,] [OUTPUT ADDPUNCT REWORD BL :WORD LAST :WORD]
OUTPUT THING BF :WORD
END

TO SET.IN
MAKE "IN.LIST FIRST BF :PAT
MAKE "PAT FPUT FIRST :PAT BF BF :PAT
END

TO SET.SPECIAL :LIST
MAKE "SPECIAL.VAR FIRST :LIST
MAKE "SPECIAL.PRED LAST :LIST
IF EMPTYP :SPECIAL.VAR [MAKE "SPECIAL.VAR "SPECIAL.BUFFER]
IF MEMBERP :SPECIAL.PRED [IN ANYOF] [SET.IN]
IF NOT EMPTYP :SPECIAL.PRED [STOP]
MAKE "SPECIAL.PRED FIRST BF :PAT
MAKE "PAT FPUT FIRST :PAT BF BF :PAT
END

TO SPECIAL :PAT :SEN
SET.SPECIAL PARSE.SPECIAL BF FIRST :PAT "
OP RUN FPUT WORD "MATCH FIRST FIRST :PAT []
END

TO TOKENIZE :TEXT
IF EMPTYP :TEXT [OUTPUT []]
OUTPUT SE TOKENWORD FIRST :TEXT " TOKENIZE BF :TEXT
END

TO TOKENWORD :WORD :OUT
IF EMPTYP :WORD [OUTPUT :OUT]
IF MEMBERP FIRST :WORD [, " ] [OUTPUT TOKENWORD BF :WORD :OUT]
IF MEMBERP FIRST :WORD [. ? ! |;|] [OUTPUT SE :OUT ".]
OUTPUT TOKENWORD BF :WORD WORD :OUT FIRST :WORD
END

TO TRANSLATE :WORD
LOCAL "NEW
MAKE "NEW GPROP :WORD "TRANSLATION
OUTPUT IFELSE EMPTYP :NEW [:WORD] [:NEW]
END

TO TRY.PRED
IF LISTP :SPECIAL.PRED [OP MATCH :SPECIAL.PRED FIRST :SEN]
OP RUN LIST :SPECIAL.PRED QUOTED FIRST :SEN
END

TO USEMEMORY
IF EMPTYP :MEMORY [LASTRESORT STOP]
PRINT FIRST :MEMORY
MAKE "MEMORY BF :MEMORY
END

Make "gensym.number 80
Make "lastresort [[I AM NOT SURE I UNDERSTAND YOU FULLY.] [PLEASE GO ON.] [WHAT DOES THAT SUGGEST TO YOU?] [DO YOU FEEL STRONGLY ABOUT DISCUSSING SUCH THINGS?]]
Make "memflag "FALSE
Pprop "alike "RULES [DIT]
Pprop "alike "PRIORITY 10
Pprop "always "g69 [[CAN YOU THINK OF A SPECIFIC EXAMPLE?] [WHEN?] [WHAT INCIDENT ARE YOU THINKING OF?] [REALLY, ALWAYS?] [WHAT IF THIS NEVER HAPPENED?]]
Pprop "always "RULES [[#] g69]
Pprop "always "PRIORITY 1
Pprop "am "g19 [[WHY DO YOU SAY "AM"?] [I DON'T UNDERSTAND THAT.]]
Pprop "am "g18 [[DO YOU BELIEVE YOU ARE :STUFF?] [WOULD YOU WANT TO BE :STUFF?] [YOU WISH I WOULD TELL YOU YOU ARE :STUFF.] [WHAT WOULD IT MEAN IF YOU WERE :STUFF?] HOW]
Pprop "am "RULES [[# ARE YOU #STUFF] g18 [#] g19]
Pprop "am "TRANSLATION "ARE
Pprop "am "PRIORITY 0
Pprop "are "g24 [[DID YOU THINK THEY MIGHT NOT BE :STUFF?] [WOULD YOU LIKE IT IF THEY WERE NOT :STUFF?] [WHAT IF THEY WERE NOT :STUFF?] [POSSIBLY THEY ARE :STUFF.]]
Pprop "are "g23 [HOW]
Pprop "are "g22 [[WHY ARE YOU INTERESTED IN WHETHER I AM :STUFF OR NOT?] [WOULD YOU PREFER IF I WEREN'T :STUFF?] [PERHAPS I AM :STUFF IN YOUR FANTASIES.] [DO YOU SOMETIMES THINK I AM :STUFF?] HOW]
Pprop "are "g21 [[WHAT MAKES YOU THINK THERE ARE :STUFF?] [DO YOU USUALLY CONSIDER :STUFF?] [DO YOU WISH THERE WERE :STUFF?]]
Pprop "are "g20 [[PRE [:A THERE ARE :B] ARE]]
Pprop "are "RULES [[#A THERE ARE #B YOU #C] g20 [# THERE ARE &STUFF] g21 [# ARE I #STUFF] g22 [ARE #] g23 [# ARE #STUFF] g24]
Pprop "are "PRIORITY 0
Pprop "ask "g80 [NEWKEY]
Pprop "ask "g79 [YOU]
Pprop "ask "g78 [HOW]
Pprop "ask "g77 [HOW]
Pprop "ask "RULES [[# YOU ASK #] g77 [# YOU ! ASKING #] g78 [# I #] g79 [#] g80]
Pprop "ask "PRIORITY 0
Pprop "because "g64 [[IS THAT THE REAL REASON?] [DON'T ANY OTHER REASONS COME TO MIND?] [DOES THAT REASON SEEM TO EXPLAIN ANYTHING ELSE?] [WHAT OTHER REASONS MIGHT THERE BE?] [YOU'RE NOT CONCEALING ANYTHING FROM ME, ARE YOU?]]
Pprop "because "RULES [[#] g64]
Pprop "because "PRIORITY 0
Pprop "believe "BELIEF "TRUE
Pprop "bet "BELIEF "TRUE
Pprop "brother "FAMILY "TRUE
Pprop "can "g60 [HOW NEWKEY]
Pprop "can "g59 [[WHETHER OR NOT YOU CAN "STUFF DEPENDS MORE ON YOU THAN ON ME.] [DO YOU WANT TO BE ABLE TO :STUFF?] [PERHAPS YOU DON'T WANT TO :STUFF.] HOW]
Pprop "can "g58 [[YOU BELIEVE I CAN :STUFF, DON'T YOU?] HOW [YOU WANT ME TO BE ABLE TO :STUFF.] [PERHAPS YOU WOULD LIKE TO BE ABLE TO :STUFF YOURSELF.]]
Pprop "can "RULES [[# CAN I #STUFF] g58 [# CAN YOU #STUFF] g59 [#] g60]
Pprop "can "PRIORITY 0
Pprop "cant "TRANSLATION "CAN'T
Pprop "certainly "RULES [YES]
Pprop "certainly "PRIORITY 0
Pprop "children "FAMILY "TRUE
Pprop "computer "g17 [[DO COMPUTERS WORRY YOU?] [WHY DO YOU MENTION COMPUTERS?] [WHAT DO YOU THINK MACHINES HAVE TO DO WITH YOUR PROBLEM?] [DON'T YOU THINK COMPUTERS CAN HELP PEOPLE?] [WHAT ABOUT MACHINES WORRIES YOU?] [WHAT DO YOU THINK ABOUT MACHINES?]]
Pprop "computer "RULES [[#] g17]
Pprop "computer "PRIORITY 50
Pprop "computers "RULES [COMPUTER]
Pprop "computers "PRIORITY 50
Pprop "dad "FAMILY "TRUE
Pprop "dad "TRANSLATION "FATHER
Pprop "daddy "FAMILY "TRUE
Pprop "daddy "TRANSLATION "FATHER
Pprop "deutsch "g15 [[I'M SORRY, I SPEAK ONLY ENGLISH.]]
Pprop "deutsch "RULES [[#] g15]
Pprop "deutsch "PRIORITY 0
Pprop "dit "g72 [[IN WHAT WAY?] [WHAT RESEMBLANCE DO YOU SEE?] [WHAT DOES THAT SIMILARITY SUGGEST TO YOU?] [WHAT OTHER CONNECTIONS DO YOU SEE?] [WHAT DO YOU SUPPOSE THAT RESEMBLANCE MEANS?] [WHAT IS THE CONNECTION, DO YOU SUPPOSE?] [COULD THERE REALLY BE SOME CONNECTION?] HOW]
Pprop "dit "RULES [[#] g72]
Pprop "dont "TRANSLATION "DON'T
Pprop "dream "g9 [[WHAT DOES THAT DREAM SUGGEST TO YOU?] [DO YOU DREAM OFTEN?] [WHAT PERSONS APPEAR IN YOUR DREAMS?] [DON'T YOU BELIEVE THAT DREAM HAS SOMETHING TO DO WITH YOUR PROBLEM?] [DO YOU EVER WISH YOU COULD FLEE FROM REALITY?] NEWKEY]
Pprop "dream "RULES [[#] g9]
Pprop "dream "PRIORITY 3
Pprop "dreamed "g8 [DREAM NEWKEY]
Pprop "dreamed "g7 [[REALLY :STUFF?] [HAVE YOU EVER FANTASIED :STUFF WHILE YOU WERE AWAKE?] [HAVE YOU DREAMED :STUFF BEFORE?] DREAM NEWKEY]
Pprop "dreamed "RULES [[# YOU DREAMED #STUFF] g7 [#] g8]
Pprop "dreamed "PRIORITY 4
Pprop "dreams "RULES [DREAM]
Pprop "dreams "PRIORITY 3
Pprop "dreams "TRANSLATION "DREAM
Pprop "dreamt "RULES [DREAMED]
Pprop "dreamt "PRIORITY 4
Pprop "dreamt "TRANSLATION "DREAMED
Pprop "espanol "RULES [DEUTSCH]
Pprop "espanol "PRIORITY 0
Pprop "everybody "RULES [EVERYONE]
Pprop "everybody "PRIORITY 2
Pprop "everyone "g68 [[REALLY, :A?] [SURELY NOT :A.] [CAN YOU THINK OF ANYONE IN PARTICULAR?] [WHO, FOR EXAMPLE?] [YOU ARE THINKING OF A VERY SPECIAL PERSON.] [WHO, MAY I ASK?] [SOMEONE SPECIAL PERHAPS.] [YOU HAVE A PARTICULAR PERSON IN MIND, DON'T YOU?] [WHO DO YOU THINK YOU'RE TALKING ABOUT?] [I SUSPECT YOU'RE EXAGGERATING A LITTLE.]]
Pprop "everyone "RULES [[# !A:IN [EVERYONE EVERYBODY NOBODY NOONE] #] g68]
Pprop "everyone "PRIORITY 2
Pprop "father "FAMILY "TRUE
Pprop "feel "BELIEF "TRUE
Pprop "francais "RULES [DEUTSCH]
Pprop "francais "PRIORITY 0
Pprop "hello "g16 [[HOW DO YOU DO. PLEASE STATE YOUR PROBLEM.]]
Pprop "hello "RULES [[#] g16]
Pprop "hello "PRIORITY 0
Pprop "how "g63 [[WHY DO YOU ASK?] [DOES THAT QUESTION INTEREST YOU?] [WHAT IS IT YOU REALLY WANT TO KNOW?] [ARE SUCH QUESTIONS MUCH ON YOUR MIND?] [WHAT ANSWER WOULD PLEASE YOU MOST?] [WHAT DO YOU THINK?] [WHAT COMES TO YOUR MIND WHEN YOU ASK THAT?] [HAVE YOU ASKED SUCH QUESTIONS BEFORE?] [HAVE YOU ASKED ANYONE ELSE?]]
Pprop "how "RULES [[#] g63]
Pprop "how "PRIORITY 0
Pprop "husband "FAMILY "TRUE
Pprop "i "g43 [[YOU SAY :STUFF.] [CAN YOU ELABORATE ON THAT?] [DO YOU SAY :STUFF FOR SOME SPECIAL REASON?] [THAT'S QUITE INTERESTING.]]
Pprop "i "g42 [[PERHAPS IN YOUR FANTASY WE :STUFF EACH OTHER.] [DO YOU WISH TO :STUFF ME?] [YOU SEEM TO NEED TO :STUFF ME.] [DO YOU :STUFF ANYONE ELSE?]]
Pprop "i "g41 [[TELL ME MORE ABOUT SUCH FEELINGS.] [DO YOU OFTEN FEEL :STUFF?] [DO YOU ENJOY FEELING :STUFF?] [OF WHAT DOES FEELING :STUFF REMIND YOU?]]
Pprop "i "g40 [[DON'T YOU REALLY :STUFF?] [WHY DON'T YOU :STUFF?] [DO YOU WISH TO BE ABLE TO :STUFF?] [DOES THAT TROUBLE YOU?]]
Pprop "i "g39 [[HOW DO YOU KNOW YOU CAN'T :STUFF?] [HAVE YOU TRIED?] [PERHAPS YOU COULD :STUFF NOW.] [DO YOU REALLY WANT TO BE ABLE TO :STUFF?]]
Pprop "i "g38 [[IS IT BECAUSE YOU ARE :STUFF THAT YOU CAME TO ME?] [HOW LONG HAVE YOU BEEN :STUFF?] [DO YOU BELIEVE IT NORMAL TO BE :STUFF?] [DO YOU ENJOY BEING :STUFF?]]
Pprop "i "g37 [YOU]
Pprop "i "g36 [[DO YOU REALLY THINK SO?] [BUT YOU ARE NOT SURE YOU :STUFF.] [DO YOU REALLY DOUBT YOU :STUFF?]]
Pprop "i "g35 [WAS]
Pprop "i "g34 [[HOW HAVE I HELPED YOU TO BE :STUFF?] [HAS YOUR TREATMENT MADE YOU :STUFF?] [WHAT MAKES YOU :STUFF JUST NOW?] [CAN YOU EXPLAIN WHY YOU ARE SUDDENLY :STUFF?] [ARE YOU SURE?] [WHAT DO YOU MEAN BY :STUFF?]]
Pprop "i "g33 [[I'M SORRY TO HEAR YOU ARE :STUFF.] [DO YOU THINK COMING HERE WILL HELP YOU NOT TO BE :STUFF?] [I'M SURE IT'S NOT PLEASANT TO BE :STUFF.] [CAN YOU EXPLAIN WHAT MADE YOU :STUFF?] [PLEASE GO ON.]]
Pprop "i "g32 [[WHAT WOULD IT MEAN TO YOU IF YOU GOT :STUFF?] [WHY DO YOU WANT :STUFF?] [SUPPOSE YOU GOT :STUFF SOON.] [WHAT IF YOU NEVER GOT :STUFF?] [WHAT WOULD GETTING :STUFF MEAN TO YOU?] [YOU REALLY WANT :STUFF.] [I SUSPECT YOU REALLY DON'T WANT :STUFF.]]
Pprop "i "RULES [[# YOU !:IN [WANT NEED] #STUFF] g32 [# YOU ARE # !STUFF:IN [SAD UNHAPPY DEPRESSED SICK] #] g33 [# YOU ARE # !STUFF:IN [HAPPY ELATED GLAD BETTER] #] g34 [# YOU WAS #] g35 [# YOU !:BELIEFP YOU #STUFF] g36 [# YOU # !:BELIEFP # I #] g37 [# YOU ARE #STUFF] g38 [# YOU !:IN [CAN'T CANNOT] #STUFF] g39 [# YOU DON'T #STUFF] g40 [# YOU FEEL #STUFF] g41 [# YOU #STUFF I #] g42 [#STUFF] g43]
Pprop "i "TRANSLATION "YOU
Pprop "i "PRIORITY 0
Pprop "i'm "g31 [[PRE [YOU ARE :STUFF] I]]
Pprop "i'm "RULES [[# YOU'RE #STUFF] g31]
Pprop "i'm "TRANSLATION "YOU'RE
Pprop "i'm "PRIORITY 0
Pprop "if "g6 [[DO YOU THINK IT'S LIKELY THAT :STUFF?] [DO YOU WISH THAT :STUFF?] [WHAT DO YOU THINK ABOUT :STUFF?]]
Pprop "if "g5 [[PRE [:A IF :B MIGHT HAVE :C] IF]]
Pprop "if "RULES [[#A IF #B HAD #C] g5 [# IF #STUFF] g6]
Pprop "if "PRIORITY 3
Pprop "is "g62 [NEWKEY]
Pprop "is "g61 [[SUPPOSE :A WERE NOT :B.] [PERHAPS :A REALLY IS :B.] [TELL ME MORE ABOUT :A.]]
Pprop "is "RULES [[&A IS &B] g61 [#] g62]
Pprop "is "PRIORITY 0
Pprop "italiano "RULES [DEUTSCH]
Pprop "italiano "PRIORITY 0
Pprop "like "g71 [NEWKEY]
Pprop "like "g70 [DIT]
Pprop "like "RULES [[# !:IN [AM IS ARE WAS] # LIKE #] g70 [#] g71]
Pprop "like "PRIORITY 10
Pprop "machine "RULES [COMPUTER]
Pprop "machine "PRIORITY 50
Pprop "machines "RULES [COMPUTER]
Pprop "machines "PRIORITY 50
Pprop "maybe "RULES [PERHAPS]
Pprop "maybe "PRIORITY 0
Pprop "me "TRANSLATION "YOU
Pprop "mom "FAMILY "TRUE
Pprop "mom "TRANSLATION "MOTHER
Pprop "mommy "FAMILY "TRUE
Pprop "mommy "TRANSLATION "MOTHER
Pprop "mother "FAMILY "TRUE
Pprop "my "g57 [NEWKEY]
Pprop "my "g56 [[YOUR :STUFF?] [WHY DO YOU SAY YOUR :STUFF?] [DOES THAT SUGGEST ANYTHING ELSE WHICH BELONGS TO YOU?] [IS IT IMPORTANT TO YOU THAT YOUR :STUFF?]]
Pprop "my "g55 [[TELL ME MORE ABOUT YOUR FAMILY.] [WHO ELSE IN YOUR FAMILY :B?] [YOUR :A?] [WHAT ELSE COMES TO MIND WHEN YOU THINK OF YOUR :A?]]
Pprop "my "RULES [[# YOUR # !A:FAMILYP #B] g55 [# YOUR &STUFF] g56 [#] g57]
Pprop "my "TRANSLATION "YOUR
Pprop "my "PRIORITY 2
Pprop "my "g12 [[EARLIER YOU SAID YOUR :STUFF.] [BUT YOUR :STUFF.] [DOES THAT HAVE ANYTHING TO DO WITH YOUR STATEMENT ABOUT :STUFF?]]
Pprop "my "MEMR [[# YOUR &STUFF] g12]
Pprop "myself "TRANSLATION "YOURSELF
Pprop "name "g14 [[I AM NOT INTERESTED IN NAMES.] [I'VE TOLD YOU BEFORE I DON'T CARE ABOUT NAMES\; PLEASE CONTINUE.]]
Pprop "name "RULES [[#] g14]
Pprop "name "PRIORITY 15
Pprop "no "g54 [[ARE YOU SAYING "NO" JUST TO BE NEGATIVE?] [YOU ARE BEING A BIT NEGATIVE.] [WHY NOT?] [WHY "NO"?] NEWKEY]
Pprop "no "g53 [XXYYZZ [PRE [X NO] NO]]
Pprop "no "RULES [[NO] g53 [#] g54]
Pprop "no "PRIORITY 0
Pprop "nobody "RULES [EVERYONE]
Pprop "nobody "PRIORITY 2
Pprop "noone "RULES [EVERYONE]
Pprop "noone "PRIORITY 2
Pprop "perhaps "g13 [[YOU DON'T SEEM QUITE CERTAIN.] [WHY THE UNCERTAIN TONE?] [CAN'T YOU BE MORE POSITIVE?] [YOU AREN'T SURE.] [DON'T YOU KNOW?]]
Pprop "perhaps "RULES [[#] g13]
Pprop "perhaps "PRIORITY 0
Pprop "problem "g76 [[EARLIER YOU MENTIONED :STUFF.] [LET'S TALK FURTHER ABOUT :STUFF.] [TELL ME MORE ABOUT :STUFF.] [YOU HAVEN'T MENTIONED :STUFF FOR A WHILE.]]
Pprop "problem "MEMR [[#STUFF IS YOUR PROBLEM #] g76]
Pprop "problem "g75 [[PLEASE CONTINUE, THIS MAY BE INTERESTING.] [HAVE YOU ANY OTHER PROBLEMS YOU WISH TO DISCUSS?] [PERHAPS YOU'D RATHER CHANGE THE SUBJECT.] [YOU SEEM A BIT UNEASY.] NEWKEY]
Pprop "problem "g74 [[YOUR :A :B :C?] [ARE YOU SURE YOUR :A :B :C?] [PERHAPS YOUR REAL :A :B NOT :C.] [YOU THINK YOU HAVE PROBLEMS?]]
Pprop "problem "g73 [[:A :B YOUR :C.] [ARE YOU SURE :A :B YOUR :C?] [PERHAPS :A :B NOT YOUR REAL :C.] [YOU THINK YOU HAVE PROBLEMS?] [DO YOU OFTEN THINK ABOUT :A?]]
Pprop "problem "RULES [[#A !B:IN [IS ARE] YOUR !C:IN [PROBLEM PROBLEMS] #] g73 [# YOUR !A:IN [PROBLEM PROBLEMS] !B:IN [IS ARE] #C] g74 [#] g75]
Pprop "problem "PRIORITY 5
Pprop "problems "RULES [PROBLEM]
Pprop "problems "PRIORITY 5
Pprop "remember "g4 [NEWKEY]
Pprop "remember "g3 [[DID YOU THINK I WOULD FORGET :STUFF?] [WHY DO YOU THINK I SHOULD RECALL :STUFF NOW?] [WHAT ABOUT :STUFF?] WHAT [YOU MENTIONED :STUFF.]]
Pprop "remember "g2 [[DO YOU OFTEN THINK OF :STUFF?] [DOES THINKING OF :STUFF BRING ANYTHING ELSE TO MIND?] [WHAT ELSE DO YOU REMEMBER?] [WHY DO YOU REMEMBER :STUFF JUST NOW?] [WHAT IN THE PRESENT SITUATION REMINDS YOU OF :STUFF?]]
Pprop "remember "RULES [[# YOU REMEMBER #STUFF] g2 [# DO I REMEMBER #STUFF] g3 [#] g4]
Pprop "remember "PRIORITY 5
Pprop "same "RULES [DIT]
Pprop "same "PRIORITY 10
Pprop "sister "FAMILY "TRUE
Pprop "sorry "g1 [[PLEASE DON'T APOLOGIZE.] [APOLOGIES ARE NOT NECESSARY.] [WHAT FEELINGS DO YOU HAVE WHEN YOU APOLOGIZE?] [I'VE TOLD YOU THAT APOLOGIES ARE NOT REQUIRED.]]
Pprop "sorry "RULES [[#] g1]
Pprop "sorry "PRIORITY 0
Pprop "svenska "RULES [DEUTSCH]
Pprop "svenska "PRIORITY 0
Pprop "think "BELIEF "TRUE
Pprop "was "g29 [NEWKEY]
Pprop "was "g28 [[WOULD YOU LIKE TO BELIEVE I WAS :STUFF?] [WHAT SUGGESTS THAT I WAS :STUFF?] [WHAT DO YOU THINK?] [PERHAPS I WAS :STUFF.] [WHAT IF I HAD BEEN :STUFF?]]
Pprop "was "g27 [[WERE YOU REALLY?] [WHY DO YOU TELL ME YOU WERE :STUFF NOW?] [PERHAPS I ALREADY KNEW YOU WERE :STUFF.]]
Pprop "was "g26 [[WHAT IF YOU WERE :STUFF?] [DO YOU THINK YOU WERE :STUFF?] [WERE YOU :STUFF?] [WHAT WOULD IT MEAN IF YOU WERE :STUFF?] [WHAT DOES " :STUFF " SUGGEST TO YOU?] HOW]
Pprop "was "RULES [[# WAS YOU #STUFF] g26 [# YOU WAS #STUFF] g27 [# WAS I #STUFF] g28 [#] g29]
Pprop "was "PRIORITY 2
Pprop "we "RULES [I]
Pprop "we "PRIORITY 0
Pprop "we "TRANSLATION "YOU
Pprop "were "RULES [WAS]
Pprop "were "TRANSLATION "WAS
Pprop "were "PRIORITY 0
Pprop "what "g11 [[TELL ME ABOUT :A :B.] [:A :B?] [DO YOU WANT ME TO TELL YOU :A :B?] [REALLY.] [I SEE.] NEWKEY]
Pprop "what "g10 [HOW]
Pprop "what "RULES [[!:IN [WHAT WHERE] #] g10 [# !A:IN [WHAT WHERE] #B] g11]
Pprop "what "PRIORITY 0
Pprop "where "RULES [HOW]
Pprop "where "PRIORITY 0
Pprop "why "g67 [[WHY INDEED?] [WHY "WHY"?] [WHY NOT?] HOW NEWKEY]
Pprop "why "g66 [[DO YOU THINK YOU SHOULD BE ABLE TO :STUFF?] [DO YOU WANT TO BE ABLE TO :STUFF?] [DO YOU BELIEVE THIS WILL HELP YOU TO :STUFF?] [HAVE YOU ANY IDEA WHY YOU CAN'T :STUFF?] HOW]
Pprop "why "g65 [[DO YOU BELIEVE I DON'T :STUFF?] [PERHAPS I WILL :STUFF IN GOOD TIME.] [SHOULD YOU :STUFF YOURSELF?] [YOU WANT ME TO :STUFF?] HOW]
Pprop "why "RULES [[# WHY DON'T I #STUFF] g65 [# WHY CAN'T YOU #STUFF] g66 [#] g67]
Pprop "why "PRIORITY 0
Pprop "wife "FAMILY "TRUE
Pprop "wish "BELIEF "TRUE
Pprop "wont "TRANSLATION "WON'T
Pprop "xxyyzz "g50 [[YOU'RE BEING SOMEWHAT SHORT WITH ME.] [YOU DON'T SEEM VERY TALKATIVE TODAY.] [PERHAPS YOU'D RATHER TALK ABOUT SOMETHING ELSE.] [ARE YOU USING MONOSYLLABLES FOR SOME REASON?] NEWKEY]
Pprop "xxyyzz "RULES [[#] g50]
Pprop "xxyyzz "PRIORITY 0
Pprop "yes "g52 [[YOU SEEM QUITE POSITIVE.] [YOU ARE SURE.] [I SEE.] [I UNDERSTAND.] NEWKEY]
Pprop "yes "g51 [XXYYZZ [PRE [X YES] YES]]
Pprop "yes "RULES [[YES] g51 [#] g52]
Pprop "yes "PRIORITY 0
Pprop "you "g49 [NEWKEY]
Pprop "you "g48 [[WE WERE DISCUSSING YOU, NOT ME.] [OH, I :STUFF?] [IS THIS REALLY RELEVANT TO YOUR PROBLEM?] [PERHAPS I DO :STUFF.] [ARE YOU GLAD TO KNOW I :STUFF?] [DO YOU :STUFF?] [WHAT ARE YOUR FEELINGS NOW?]]
Pprop "you "g47 [[WHY DO YOU THINK I :STUFF YOU?] [YOU LIKE TO THINK I :STUFF YOU, DON'T YOU?] [WHAT MAKES YOU THINK I :STUFF YOU?] [REALLY, I :STUFF YOU?] [DO YOU WISH TO BELIEVE I :STUFF YOU?] [SUPPOSE I DID :STUFF YOU. WHAT WOULD THAT MEAN?] [DOES SOMEONE ELSE BELIEVE I :STUFF YOU?]]
Pprop "you "g46 [[WHAT MAKES YOU THINK I AM :STUFF?] [DOES IT PLEASE YOU TO BELIEVE I AM :STUFF?] [PERHAPS YOU WOULD LIKE TO BE :STUFF.] [DO YOU SOMETIMES WISH YOU WERE :STUFF?]]
Pprop "you "g45 [NEWKEY]
Pprop "you "g44 [DIT]
Pprop "you "RULES [[# I REMIND YOU OF #] g44 [# I ARE # YOU #] g45 [# I # ARE #STUFF] g46 [# I #STUFF YOU] g47 [# I &STUFF] g48 [#] g49]
Pprop "you "TRANSLATION "I
Pprop "you "PRIORITY 0
Pprop "you're "g30 [[PRE [I ARE :STUFF] YOU]]
Pprop "you're "RULES [[# I'M #STUFF] g30]
Pprop "you're "TRANSLATION "I'M
Pprop "you're "PRIORITY 0
Pprop "your "g25 [[WHY ARE YOU CONCERNED OVER MY :STUFF?] [WHAT ABOUT YOUR OWN :STUFF?] [ARE YOU WORRIED ABOUT SOMEONE ELSE'S :STUFF?] [REALLY, MY :STUFF?]]
Pprop "your "RULES [[# MY #STUFF] g25]
Pprop "your "TRANSLATION "MY
Pprop "your "PRIORITY 0
Pprop "yourself "TRANSLATION "MYSELF
